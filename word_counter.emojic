📦 files 🏠

🏁 🍇


  🎞🐇💻❗ ➡ args

  ↪️ 🐔args❗ ◀ 2 🍇
     🤯🐇💻 🔤No argument🔤 ❗
  🍉  

  🐽 args 1 ❗ ➡f_name
  
  📜🕊📑 f_name ❗ ➡ file_exists
  ❎file_exists ❗ ➡ file_not_readable

  ↪️ file_not_readable 🍇
    🤯🐇💻 🔤file not readable🔤 ❗
  🍉

  🍺📇🐇📄 f_name ❗ ➡ raw_bytes
  
  🖍🆕 file_string 🍬🔡
  
  💭 This should have checking that a value was returned
  🔡raw_bytes ❗ ➡ 🖍file_string

  ↪️ file_string 🙌 🤷‍♀️ 🍇
   🤯🐇💻🔤Could not convert bytes to string🔤❗️
 🍉
  
  🔫 🍺file_string 🔤❌n🔤❗ ➡ line_strings

  🆕🍨🐚🔡🍆🐸 ❗ ➡ 🖍🆕cleaned_strings

  🔂string line_strings 🍇
    🔫 string 🔤 🔤❗ ➡ cleaned
    🔂 clean cleaned 🍇
      🐻 cleaned_strings clean  ❗
    🍉
  🍉

  🆕🍯🐚🔢🍆🐸❗ ➡ 🖍🆕thecount
  
  🔂 text cleaned_strings🍇
    ↪️ 🐣 thecount text ❗ 🍇
      🐽 thecount text ❗ ➡  prev
      🍺 prev  ➡  prev_val
      prev_val ➕ 1 ➡  now
      now ➡ 🐽 thecount text ❗
    🍉
    🙅 🍇
         1 ➡ 🐽thecount text❗
    🍉
  🍉

  🐨 thecount 🔤🔤 ❗

  🐙 thecount ❗ ➡ keys  

  🔂 key keys 🍇
    🐽 thecount key ❗ ➡ num
    🍺 num ➡  num_val
    🔡 num_val 10 ❗➡val_str
    😀 🍪 key 🔤 🔤 val_str 🍪 ❗
  🍉
  
🍉