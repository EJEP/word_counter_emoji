📦 files 🏠

🏁 🍇


  🎞🐇💻❗ ➡ args
  
  🐽 args 1 ❗ ➡f_name
  
  💭 Need code to check that this was worked
  💭 Ought to check that the file exists as well

  📜🕊📑 f_name ❗ ➡ file_exists
  ❎file_exists ❗ ➡ file_not_readable

  ↪️ file_not_readable 🍇
    🤯🐇💻 🔤file not readable🔤 ❗
  🍉

  🍺📇🐇📄 f_name ❗ ➡ raw_bytes
  
  🖍🆕 file_string 🍬🔡
  
  💭 This should have checking that a value was returned
  🔡raw_bytes ❗ ➡ 🖍file_string
  
  🔫 🍺file_string 🔤❌n🔤❗ ➡ line_strings

  🆕🍨🐚🔡🍆🐸 ❗ ➡ 🖍🆕cleaned_strings

  🔂string line_strings 🍇
    🔫 string 🔤 🔤❗ ➡ cleaned
    🔂 clean cleaned 🍇
      🐻 cleaned_strings clean  ❗
    🍉
  🍉

  🆕🍯🐚🔢🍆🐸❗ ➡ 🖍🆕thecount
  
  🔂 text cleaned_strings🍇
    ↪️ 🐣 thecount text ❗ 🍇
      🐽 thecount text ❗ ➡  prev
      🍺 prev  ➡  prev_val
      prev_val ➕ 1 ➡  now
      now ➡ 🐽 thecount text ❗
    🍉
    🙅 🍇
         1 ➡ 🐽thecount text❗
    🍉
  🍉

  🐨 thecount 🔤🔤 ❗

  🐙 thecount ❗ ➡ keys  

  🔂 key keys 🍇
    🐽 thecount key ❗ ➡ num
    🍺 num ➡  num_val
    🔡 num_val 10 ❗➡val_str
    😀 🍪 key 🔤 🔤 val_str 🍪 ❗
  🍉
  
🍉