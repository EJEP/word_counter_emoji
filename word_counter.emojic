📦 files 🏠

🐇☢🍇
  🖍🆕 word 🔡
  🖍🆕frequency🔢

  🆕 🍼 word 🔡 🍼 frequency🔢 🍇🍉

  ❗🐾🍇
    🔡 frequency 10 ❗ ➡ freq_str
    😀 🍪word 🔤: 🔤 freq_str🍪 ❗
  🍉

  ❗🥶➡🔢🍇
    ↩️ frequency
  🍉

  ❗🧙‍♀️➡🔡🍇
    ↩️ word
  🍉
🍉

🏁 🍇

  🍇freq1 ☢ freq2☢ ➡🔢
    ↪️ 🥶freq1❗ ◀ 🥶freq2❗ 🍇
      ↩️1
    🍉
    🙅↪️ 🥶freq1❗ ▶ 🥶freq2❗ 🍇
      ↩️-1
    🍉
    🙅 🍇
      📇🧙‍♀️freq1❗❗➡f1_utf
      📇🧙‍♀️freq2❗❗➡f2_utf
      0 ➡🖍🆕count
      🐔f2_utf ❗ ➡ n_f2_bytes
      🔂 byte1 f1_utf 🍇
        ↪️ count ▶ n_f2_bytes 🍇
          ↩️1
        🍉
        🙅↪️ byte1 ◀ 🐽f2_utf count❗ 🍇
          ↩️-1
        🍉
        🙅↪️ byte1 ▶ 🐽f2_utf count❗ 🍇
          ↩️1
        🍉        

        count ➡ old_count
        old_count ➕ 1 ➡ 🖍count
        
      🍉

      ↩️0
    🍉
    
  🍉 ➡ comp

  🎞🐇💻❗ ➡ args

  ↪️ 🐔args❗ ◀ 2 🍇
     🤯🐇💻 🔤No argument🔤 ❗
  🍉  

  🐽 args 1 ❗ ➡f_name
  
  📜🕊📑 f_name ❗ ➡ file_exists
  ❎file_exists ❗ ➡ file_not_readable

  ↪️ file_not_readable 🍇
    🤯🐇💻 🔤file not readable🔤 ❗
  🍉

  🍺📇🐇📄 f_name ❗ ➡ raw_bytes
  
  🖍🆕 file_string 🍬🔡
  
  💭 This should have checking that a value was returned
  🔡raw_bytes ❗ ➡ 🖍file_string

  ↪️ file_string 🙌 🤷‍♀️ 🍇
   🤯🐇💻🔤Could not convert bytes to file🔤❗️
 🍉
  
  🔫 🍺file_string 🔤❌n🔤❗ ➡ line_strings

  🆕🍨🐚🔡🍆🐸 ❗ ➡ 🖍🆕cleaned_strings

  🔂string line_strings 🍇
    🔫 string 🔤 🔤❗ ➡ cleaned
    🔂 clean cleaned 🍇
      🐻 cleaned_strings clean  ❗
    🍉
  🍉

  🆕🍯🐚🔢🍆🐸❗ ➡ 🖍🆕thecount

  🍯 🔤one🔤 1 🔤two🔤 2 🔤three🔤 3 🔤four🔤 4 🔤five🔤 5 🔤six🔤 6 🔤seven🔤 7 🔤eight🔤 8 🔤nine🔤 9  🍆 ➡️ numbers
  🔤🔤➡🖍🆕previous_word

  🔂 text cleaned_strings🍇
   ↪️ 🐣 numbers text ❗ 🍇
      🍺🐽 numbers text ❗ ➖ 1 ➡ to_add
      ↪️ 🐣 thecount previous_word ❗🍇
         🍺🐽 thecount previous_word ❗ ➡  prev
          prev ➕ to_add ➡  now
          now ➡ 🐽 thecount previous_word ❗
      🍉
   🍉

   🙅↪️ 🐣 thecount text ❗ 🍇
      🐽 thecount text ❗ ➡  prev
      🍺 prev  ➡  prev_val
      prev_val ➕ 1 ➡  now
      now ➡ 🐽 thecount text ❗
      text➡🖍previous_word
    🍉
    🙅 🍇
         1 ➡ 🐽thecount text❗
         text➡🖍previous_word
    🍉
  🍉

  🐨 thecount 🔤🔤 ❗

  🐙 thecount ❗ ➡ keys  

  🆕🍨🐚☢🍆🐸 ❗ ➡ 🖍🆕frequencies
  
  🔂 key keys 🍇
    🐽 thecount key ❗ ➡ num
    🍺 num ➡  num_val
    🆕☢🆕 key num_val ❗ ➡ freq
    🐻frequencies freq❗
  🍉

  🦁frequencies comp❗

  🔂 frequency frequencies 🍇
    🐾frequency❗
  🍉

  🐔frequencies❗➡num_words

  🖍🆕words_to_print🔢
  ↪️ num_words ▶ 5🍇
    5 ➡🖍words_to_print
  🍉
  🙅🍇
    num_words ➡🖍 words_to_print
  🍉

  🔂 i 🆕⏩⏩ 0 words_to_print❗️ 🍇
    🐽 frequencies i ❗➡ info
    🐾info❗
  🍉
🍉
